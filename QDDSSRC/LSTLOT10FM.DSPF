     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP('PGMF01' PGMF01)
     A                                      HLPTITLE('Aide générale -
     A                                      sur PGMF01')
     A                                      ALTHELP(CA01)
     A                                      INDARA
     A                                      REF(LOTS)
     A          R SFL01                     SFL
     A  08                                  SFLNXTCHG
     A            ZOPT           2   B  8  2
     A  20                                  DSPATR(PR)
     A            HROWIDLOTS    10  0H
     A            HTOUT          1   H
     A            HMATR         10  0H
     A            ZLOTS     R        O  8  6REFFLD(LOTSF/NUMLOT *LIBL/LOTS)
     A            ZDESCLOT  R        O  9 14REFFLD(LOTSF/DESCLOT *LIBL/LOTS)
     A            ZLISTBIBPRR        O  8 14REFFLD(LOTSF/LISTBIBPRJ *LIBL/LOTS)
     A            ZBIBLOT   R        O  8 26REFFLD(LOTSF/BIBLOT *LIBL/LOTS)
     A          R FORE1                     SFLCTL(SFL01)
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A N07                                  ROLLUP(02)
     A                                      CSRLOC(WLIGN WCOLN)
     A                                      CHANGE(91)
     A N04                                  ERASE(SFL01)
     A  04                                  SFLDSP
     A  05                                  SFLDSPCTL
     A  06                                  SFLCLR
     A  07                                  SFLEND
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A                                      SFLCSRRRN(&WRACSR)
     A            WRACSR         5S 0H
     A            WRAN01         4S 0H      SFLRCDNBR
     A            ZPGM          10   O  1  2
     A                                      OVRDTA
     A                                  1 31'Liste du personnel'
     A                                      COLOR(WHT)
     A            ZUSER         10   O  1 70
     A                                      OVRDTA
     A            ZDATE          8  0O  2  2
     A                                      EDTWRD('  /  /    ')
     A                                      OVRDTA
     A            ZHEUR          6  0O  2 14
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            ZJOB          10   O  2 70
     A                                      OVRDTA
     A                                  4  3'Indiquez vos options, puis appuyez-
     A                                       sur ENTREE.'
     A                                      COLOR(BLU)
     A                                  5  3'2=Modifier  3=Copier  4=Supprimer -
     A                                       5=Afficher  6=Imprimer  12=Gérer l-
     A                                      e lot'
     A                                      COLOR(BLU)
     A                                  6  3'La description contient :'
     A            ZSDESCLOT R        B  6 29REFFLD(LOTSF/DESCLOT *LIBL/LOTS)
     A                                      CHECK(LC)
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  7  2'Opt Numéro  Liste bib   Bib. lot'
     A                                      COLOR(WHT)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A          R FORB1
     A                                      CF03
     A                                      CF05
     A                                      CF06
     A                                      CF08
     A                                      CF12
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CSRLOC(WLIGN WCOLN)
     A                                      CHANGE(91)
     A                                      HLPCLR
     A          H                           HLPARA(6 23 6 58)
     A                                      HLPPNLGRP('ZSNOM' PGMF01)
     A          H                           HLPARA(8 2 22 3)
     A                                      HLPPNLGRP('ZOPT' PGMF01)
     A                                 23  3'F3=Exit  F5=Réafficher  -
     A                                      F6=Créer  F8=Imprimer  -
     A                                      F12=Annuler'
     A                                      COLOR(BLU)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A          R WSFMSG                    SFL
     A                                      SFLMSGRCD(24)
     A            WMGKEY                    SFLMSGKEY
     A            WPGMQ                     SFLPGMQ
     A          R WSFCTL                    SFLCTL(WSFMSG)
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A  10                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  09                                  SFLEND
     A            WPGMQ                     SFLPGMQ
