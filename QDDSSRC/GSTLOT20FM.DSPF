     /* >>PRE-COMPILER<<                                                 */
     /*   >>CRTCMD<<  CRTDSPF FILE(&LI/&OB) SRCFILE(&SL/&SF) +           */
     /*                 SRCMBR(&SM);                                     */
     /*   >>IMPORTANT<<                                                  */
     /*     >>PARM<<  OPTION(*EVENTF);                                   */
     /*     >>PARM<<  RSTDSP(*YES);                                      */
     /*   >>END-IMPORTANT<<                                              */
     /*   >>EXECUTE<<                                                    */
     /* >>END-PRE-COMPILER<<                                             */
     A                                      DSPSIZ(*DS4)
     A                                      MSGLOC(27)
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP('PGMF02' PGMF02)
     A                                      HLPTITLE('Détail personnel')
     A                                      ALTHELP(CA01)
     A                                      INDARA
     A          R FORE1
     A                                      CF03
     A                                      CF04
     A                                      CF12
     A                                      CF15
     A                                      RTNCSRLOC(&WFMCUR &WZOCUR &WPRCUR)
     A                                      CSRLOC(WNMLGN WNMCOL)
     A                                      CHANGE(91)
     A                                      HLPCLR
     A          H
     A                                      HLPARA(4 11 4 19)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A          H
     A                                      HLPARA(5 11 5 60)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A          H
     A                                      HLPARA(4 88 4 98)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A          H
     A                                      HLPARA(5 88 5 98)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A          H
     A                                      HLPARA(7 3 22 130)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A            ZPGM          10   O  1  2
     A                                  1 61'Détail Projet'
     A                                      COLOR(WHT)
     A            ZDATE           L  O  2  2DATFMT(*EUR)
     A            ZHEUR           T  O  2 13TIMFMT(*EUR)
     A            ZMODE         30   O  2 53
     A                                      COLOR(WHT)
     A            ZJOB          10   O  2122
     A                                 25  4'Crée le :'
     A            ZDAC            L  O 25 16DATFMT(*EUR)
     A                                      COLOR(GRN)
     A                                 25 28'à :'
     A            ZHEC            T  O 25 32TIMFMT(*EUR)
     A                                 25 42'par :'
     A            ZUSC          10   O 25 48
     A                                 25 62'Modifié le :'
     A            ZDAM            L  O 25 77DATFMT(*EUR)
     A                                      COLOR(GRN)
     A                                 25 89'à :'
     A            ZHEM            T  O 25 93TIMFMT(*EUR)
     A                                 25103'par :'
     A            ZUSM          10   O 25109
     A                                 26  2'F3=Exit   F4=Invite  F12=Annuler'
     A                                      COLOR(BLU)
     A            WFMCUR        10A  H
     A            WZOCUR        10A  H
     A            WPRCUR         4S 0H
     A            WNMLGN         3S 0H
     A            WNMCOL         3S 0H
     A                                  3  3'Numéro'
     A                                  4  3'Titre'
     A                                  6  3'Description du projet'
     A            ZNUMLOT   R        O  3 11REFFLD(LOTSF/NUMLOT *LIBL/LOTS)
     A                                      COLOR(WHT)
     A            ZLIBELLOT R        B  5  3REFFLD(LOTSF/LIBELLOT *LIBL/LOTS)
     A                                      COLOR(WHT)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            ZDESCRIPL R        B  7  3REFFLD(LOTSF/DESCRIPLOT *LIBL/LOTS)
     A                                      CNTFLD(128)
     A                                      COLOR(WHT)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  3 67'Bibliothèque dédiée'
     A            ZBIBLOT   R        O  3 89REFFLD(LOTSF/BIBLOT *LIBL/LOTS)
     A                                      COLOR(WHT)
     A                                  3 24'Liste de bibliothèques'
     A            ZLISTBIBPRR        B  3 48REFFLD(LOTSF/LISTBIBPRJ *LIBL/LOTS)
     A                                      COLOR(WHT)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PR)
     A  25                                  DSPATR(RI PC)
     A                                  3103'Statut'
     A            ZSTATLOT      20   O  3111
     A                                      COLOR(WHT)
     A                                 23  3'Lien Ticket'
     A                                 24  3'Lien GIT'
     A            ZLNKTICKET   116   B 23 15
     A                                      COLOR(WHT)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            ZLNKGIT      116   B 24 15COLOR(WHT)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PR)
     A                                      CHECK(LC)
     A          R WMGSFL                    SFL
     A                                      SFLMSGRCD(27)
     A            WMGKEY                    SFLMSGKEY
     A            WPGMQ                     SFLPGMQ
     A          R WMGCTL                    SFLCTL(WMGSFL)
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A  10                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  09                                  SFLEND
     A            WPGMQ                     SFLPGMQ
