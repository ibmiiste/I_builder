     /* >>PRE-COMPILER<<                                                 */
     /*   >>CRTCMD<<  CRTDSPF FILE(&LI/&OB) SRCFILE(&SL/&SF) +           */
     /*                 SRCMBR(&SM);                                     */
     /*   >>IMPORTANT<<                                                  */
     /*     >>PARM<<  OPTION(*EVENTF);                                   */
     /*   >>END-IMPORTANT<<                                              */
     /*   >>EXECUTE<<                                                    */
     /* >>END-PRE-COMPILER<<                                             */
     A                                      DSPSIZ(*DS4)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP('PGMF02' PGMF02)
     A                                      HLPTITLE('Détail personnel')
     A                                      ALTHELP(CA01)
     A                                      INDARA
     A          R FORE1
     A                                      CF03
     A                                      CF04
     A                                      CF12
     A                                      CF15
     A                                      RTNCSRLOC(&WFMCUR &WZOCUR &WPRCUR)
     A                                      PUTOVR
     A                                      CSRLOC(WNMLGN WNMCOL)
     A                                      CHANGE(91)
     A                                      HLPCLR
     A          H
     A                                      HLPARA(4 11 4 16)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A          H
     A                                      HLPARA(5 11 5 60)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A          H
     A                                      HLPARA(4 88 4 98)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A          H
     A                                      HLPARA(5 88 5 98)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A          H
     A                                      HLPARA(7 3 22 130)
     A                                      HLPPNLGRP(FORE1 GSTPRJ20)
     A            ZPGM          10   O  1  2
     A                                      OVRDTA
     A                                  1 61'Détail Projet'
     A                                      COLOR(WHT)
     A            ZDATE           L  O  2  2DATFMT(*EUR) OVRDTA
     A            ZHEUR           T  O  2 13TIMFMT(*EUR) OVRDTA
     A            ZMODE         30   O  2 53
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A            ZJOB          10   O  2122
     A                                      OVRDTA
     A                                 25  6'Crée le :'
     A            ZDAC            L  O 25 18DATFMT(*EUR) OVRDTA
     A                                      COLOR(GRN)
     A                                 25 30'à :'
     A            ZHEC            T  O 25 34TIMFMT(*EUR) OVRDTA
     A                                 25 44'par :'
     A            ZUSC          10   O 25 50
     A                                      OVRDTA
     A                                 26  3'Modifié le :'
     A            ZDAM            L  O 26 18DATFMT(*EUR) OVRDTA
     A                                      COLOR(GRN)
     A                                 26 30'à :'
     A            ZHEM            T  O 26 34TIMFMT(*EUR) OVRDTA
     A                                 26 44'par :'
     A            ZUSM          10   O 26 50
     A                                      OVRDTA
     A                                 27  2'F3=Exit   F4=Invite  F12=Annuler'
     A                                      COLOR(BLU)
     A            WFMCUR        10A  H
     A            WZOCUR        10A  H
     A            WPRCUR         4S 0H
     A            WNMLGN         3S 0H
     A            WNMCOL         3S 0H
     A                                  4  3'Numéro'
     A                                  5  3'Titre'
     A                                  6  3'Description du projet'
     A            ZNUMLOT   R        O  4 11REFFLD(LOTSF/NUMLOT *LIBL/LOTS)
     A                                      COLOR(WHT)
     A            ZLIBELLOT R        B  5 11REFFLD(LOTSF/LIBELLOT *LIBL/LOTS)
     A                                      COLOR(WHT)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PR)
     A            ZDESCRIPL R        B  7  3REFFLD(LOTSF/DESCRIPLOT *LIBL/LOTS)
     A                                      CNTFLD(128)
     A                                      COLOR(WHT)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PR)
     A                                  4 67'Bibliothèque dédiée'
     A            ZBIBLOT   R        O  4 89REFFLD(LOTSF/BIBLOT *LIBL/LOTS)
     A                                      COLOR(WHT)
     A                                  5 67'Liste de biblothèques'
     A            ZLISTBIBPRR        B  5 89REFFLD(LOTSF/LISTBIBPRJ *LIBL/LOTS)
     A                                      COLOR(WHT)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PR)
     A                                  4103'Statut'
     A            ZSTATLOT      20   O  4111
     A                                      COLOR(WHT)
     A            ZROWIDLOTS    84   O 24  4COLOR(WHT)
     A                                      NOCCSID
     A          R WMGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            WMGKEY                    SFLMSGKEY
     A            WPGMQ                     SFLPGMQ
     A          R WMGCTL                    SFLCTL(WMGSFL)
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A  10                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  09                                  SFLEND
     A            WPGMQ                     SFLPGMQ
