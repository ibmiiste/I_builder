     /* >>PRE-COMPILER<<                                                 */
     /*   >>CRTCMD<<  CRTDSPF FILE(&LI/&OB) SRCFILE(&SL/&SF) +           */
     /*                 SRCMBR(&SM);                                     */
     /*   >>IMPORTANT<<                                                  */
     /*     >>PARM<<  OPTION(*EVENTF);                                   */
     /*     >>PARM<<  RSTDSP(*YES);                                      */
     /*   >>END-IMPORTANT<<                                              */
     /*   >>EXECUTE<<                                                    */
     /* >>END-PRE-COMPILER<<                                             */
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      PRINT
     A                                      HELP
     A                                      HLPPNLGRP('PGMF01' PGMF01)
     A                                      HLPTITLE('Aide générale -
     A                                      sur PGMF01')
     A                                      ALTHELP(CA01)
     A                                      INDARA
     A                                      REF(LOTS)
     A          R SFL01                     SFL
     A  08                                  SFLNXTCHG
     A            ZOPT           2   B  8  2
     A  20                                  DSPATR(PR)
     A            HNREC         10  0H
     A            ZNUMLOT   R        O  8  5REFFLD(LOTSF/NUMLOT *LIBL/LOTS)
     A                                      EDTCDE(Z)
     A  21                                  COLOR(WHT)
     A N21                                  COLOR(GRN)
     A N21                                  DSPATR(HI)
     A            ZLIBELLOT R   81   O  8 15REFFLD(LOTSF/LIBELLOT I_BUILDER/LOT-
     A                                      S)
     A  21                                  COLOR(WHT)
     A N21                                  COLOR(GRN)
     A N21                                  DSPATR(HI)
     A            ZLISTBIBPRR        O  8 97REFFLD(LOTSF/LISTBIBPRJ *LIBL/LOTS)
     A  21                                  COLOR(WHT)
     A N21                                  COLOR(GRN)
     A N21                                  DSPATR(HI)
     A            ZBIBLOT   R        O  8108REFFLD(LOTSF/BIBLOT *LIBL/LOTS)
     A  21                                  COLOR(WHT)
     A N21                                  COLOR(GRN)
     A N21                                  DSPATR(HI)
     A            ZSTATLOT      13   O  8119
     A  21                                  COLOR(WHT)
     A N21                                  COLOR(GRN)
     A N21                                  DSPATR(HI)
     A            HSTATENREG     1N 0H
     A          R FORE1                     SFLCTL(SFL01)
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A N07                                  ROLLUP(02)
     A                                      CSRLOC(WLIGN WCOLN)
     A                                      CHANGE(91)
     A N04                                  ERASE(SFL01)
     A  04                                  SFLDSP
     A  05                                  SFLDSPCTL
     A  06                                  SFLCLR
     A  07                                  SFLEND
     A                                      SFLSIZ(9996)
     A                                      SFLPAG(17)
     A                                      SFLCSRRRN(&WRACSR)
     A            WRACSR         5S 0H
     A            WRAN01         4S 0H      SFLRCDNBR
     A            ZPGM          10   O  1  2
     A                                      OVRDTA
     A                                  1 58'Gestion des lots'
     A                                      COLOR(WHT)
     A            ZUSER         10   O  1122
     A                                      OVRDTA
     A            ZJOB          10   O  2122
     A                                      OVRDTA
     A                                  4  3'Indiquez vos options, puis appuyez-
     A                                       sur ENTREE.'
     A                                      COLOR(BLU)
     A                                  5  3'2=Modifier  3=Copier  4=Supprimer -
     A                                       5=Afficher  6=Imprimer  12=Gérer l-
     A                                      e lot'
     A                                      COLOR(BLU)
     A                                  6  3'La description contient (REGEXP) :'
     A            ZSLIBELLOTR   50   B  6 38REFFLD(LOTSF/LIBELLOT *LIBL/LOTS)
     A                                      CHECK(LC)
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                  7  2'Opt'
     A                                      COLOR(WHT)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A            ZDATE           L  O  2  2DATFMT(*EUR)
     A            ZHEUR           T  O  2 13TIMFMT(*EUR)
     A                                  7  6'Numéro'
     A                                      COLOR(WHT)
     A                                  7 15'Sujet du lot'
     A                                      COLOR(WHT)
     A                                  7 97'Liste bib'
     A                                      COLOR(WHT)
     A                                  7108'Bib. lot'
     A                                      COLOR(WHT)
     A                                  7119'Statut'
     A                                      COLOR(WHT)
     A          R FORB1
     A                                      CF05
     A                                      CF06
     A                                      CF08
     A                                      CF12
     A                                      RTNCSRLOC(&WLICSR &WZOCSR &WPRCSR)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CSRLOC(WLIGN WCOLN)
     A                                      CHANGE(91)
     A                                      HLPCLR
     A                                      CF09
     A                                      CA03
     A          H                           HLPARA(6 23 6 58)
     A                                      HLPPNLGRP('ZSNOM' PGMF01)
     A          H                           HLPARA(8 2 22 3)
     A                                      HLPPNLGRP('ZOPT' PGMF01)
     A                                 26  3'F3=Exit'
     A                                      COLOR(BLU)
     A            WLICSR        10A  H
     A            WZOCSR        10A  H
     A            WPRCSR         4S 0H
     A            WLIGN          3S 0H
     A            WCOLN          3S 0H
     A                                 26 11'F5=Réafficher'
     A                                      COLOR(BLU)
     A                                 26 25'F6=Créer'
     A                                      COLOR(BLU)
     A                                 26 34'F8=Imprimer'
     A                                      COLOR(BLU)
     A  22                             26 46'F9=Lots en cours'
     A                                      COLOR(BLU)
     A N22                             26 46'F9=Tous les lots'
     A                                      COLOR(BLU)
     A                                 26 63'F12=Précédent'
     A                                      COLOR(BLU)
     A          R WSFMSG                    SFL
     A                                      SFLMSGRCD(27)
     A            WMGKEY                    SFLMSGKEY
     A            WPGMQ                     SFLPGMQ
     A          R WSFCTL                    SFLCTL(WSFMSG)
     A                                      SFLSIZ(2)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A  10                                  SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  09                                  SFLEND
     A            WPGMQ                     SFLPGMQ
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="Gestion des lots">
     A*%%RS+  <device type="display" width="132" height="27" />
     A*%%RS+  <record-write record-format="SFL01">
     A*%%RS+   <field-data>
     A*%%RS+    <field name="ZBIBLOT" value="LOT_000160" />
     A*%%RS+    <field name="ZLISTBIBPR" value="MIR_CICA95" />
     A*%%RS+    <field name="ZNUMLOT" value="1" />
     A*%%RS+    <field name="ZOPT" value="2" />
     A*%%RS+    <field name="ZLIBELLOT" value="Projet MIRUS" />
     A*%%RS+   </field-data>
     A*%%RS+  </record-write>
     A*%%RS+  <record-write record-format="FORE1" />
     A*%%RS+  <record-write record-format="FORB1" />
     A*%%RS+  <record-write record-format="WSFMSG" />
     A*%%RS+  <record-write record-format="WSFCTL" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
