      /* >>PRE-COMPILER<<                                                 */
      /*   >>CRTCMD<<  CRTCLMOD MODULE(&LI/&OB) SRCFILE(&SL/&SF) +        */
      /*                 SRCMBR(&SM);                                     */
      /*   >>IMPORTANT<<                                                  */
      /*     >>PARM<<  OPTION(*EVENTF);                                   */
      /*     >>PARM<<  DBGVIEW(*ALL);                                     */
      /*   >>END-IMPORTANT<<                                              */
      /*   >>EXECUTE<<                                                    */
      /* >>END-PRE-COMPILER<<                                             */
             PGM        PARM(&NUMLOT &MACHINE &USER &MDP)

             DCL        VAR(&NUMLOT) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&NUMLOTC) TYPE(*CHAR) LEN(9)
             DCL        VAR(&BIBLIO) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MACHINE) TYPE(*CHAR) LEN(32)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MDP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(2000)
             DCL        VAR(&SAVF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CHEMINSAVO) TYPE(*CHAR) LEN(256)
             DCL        VAR(&CHEMINSAVD) TYPE(*CHAR) LEN(256)
             DCL        VAR(&PTYP) TYPE(*CHAR) LEN(1) VALUE('5')
             DCL        VAR(&PFICM) TYPE(*CHAR) LEN(10) VALUE('BUILDMSGF')
             DCL        VAR(&PMGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PMGDT) TYPE(*CHAR) LEN(99)
             DCL        VAR(&SRCF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10) VALUE('GSTLOT20')
             DCL        VAR(&REQUETE) TYPE(*CHAR) LEN(1024)

             DCLPRCOPT  ALWRTVSRC(*YES) TEXT('Préparation à la livraison et +
                          livraison') BNDDIR(I_BUILDER)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERREUR))

             CHGVAR     VAR(&NUMLOTC) VALUE(&NUMLOT)

             CHGVAR     VAR(&BIBLIO) VALUE('L' *TCAT &NUMLOTC)

             CRTDUPOBJ  OBJ(DSTLOT10) FROMLIB(I_BUILDER) +
                          OBJTYPE(*PGM) TOLIB(&BIBLIO)

             /* Copier liste des objets à livrer dans une table dans le lot Séquence/Bib */
             /* source/Fichier source/membre source/bib objet */
             CHGVAR     VAR(&REQUETE) VALUE('CREATE TABLE' *BCAT &BIBLIO *TCAT +
                          '.LSTOBJBLD AS ( SELECT ORDRE, BIBLIOTHEQUE_DESTINATION, +
                          OBJET_DESTINATION, TYPE_OBJET FROM I_BUILDER.LSTOBJBLD +
                          WHERE LOT_NUMBER =' *BCAT &NUMLOTC *TCAT ') WITH DATA')
             RUNSQL     SQL(&REQUETE) COMMIT(*NONE) NAMING(*SQL)


             /* Création de la SAVF qui va être envoyée via FTP               */
             CHGVAR     VAR(&SAVF) VALUE('S' *TCAT &NUMLOTC)
             CRTSAVF    FILE(&BIBLIO/&SAVF)

             /* Création de la bibliothèque Lot sur le système cible          */
             CHGVAR     VAR(&CMD) VALUE('CRTLIB LIB(' *TCAT &BIBLIO *TCAT ')')
             RUNRMTCMD  CMD(&CMD) RMTLOCNAME(&MACHINE *IP) RMTUSER(&USER) +
                          RMTPWD(&MDP)
             MONMSG     MSGID(CPF91CF) EXEC(DO)
                CHGVAR     VAR(&PMGDT) VALUE(&MACHINE)
                CHGVAR     VAR(&PMGID) VALUE('ERR0002')
                CALL       PGM(GSTMSG01) PARM(&PTYP &PFICM &PMGID &PMGDT &PGM)
                GOTO       CMDLBL(FIN)
             ENDDO

             /* Créer une SAVF comportant le numéro du lot sur le système distant */
             CHGVAR     VAR(&CMD) VALUE('CRTSAVF FILE(' *TCAT &BIBLIO *TCAT '/' +
                          *TCAT &SAVF *TCAT ')')
             RUNRMTCMD  CMD(&CMD) RMTLOCNAME(&MACHINE *IP) RMTUSER(&USER) +
                          RMTPWD(&MDP)
             MONMSG     MSGID(CPF91CF) EXEC(DO)
                CHGVAR     VAR(&PMGDT) VALUE(&MACHINE)
                CHGVAR     VAR(&PMGID) VALUE('ERR0003')
                CALL       PGM(GSTMSG01) PARM(&PTYP &PFICM &PMGID &PMGDT &PGM)
                GOTO       CMDLBL(FIN)
             ENDDO

             /* Sauvegarde des objets du lots dans une SAVF                               */
             CALLPRC    PRC(SAUV_OBJ_LOT) PARM((&NUMLOT))

             /* Envoyer la SAVF sur le système cible par ftp dans la bib lot              */
             CHGVAR     VAR(&CHEMINSAVO) VALUE('/QSYS.LIB/' *TCAT &BIBLIO *TCAT +
                          '.LIB')
             CHGVAR     VAR(&CHEMINSAVD) VALUE(&CHEMINSAVO)

             CALLPRC    PRC(SCRIPTFTP) PARM((&USER) (&MDP) (&SAVF) (&SRCF) +
                          (&CHEMINSAVO) (&CHEMINSAVD))

             OVRDBF     FILE(INPUT) TOFILE(&SRCF) MBR(IN)
             OVRDBF     FILE(OUTPUT) TOFILE(&SRCF) MBR(OUT)

             FTP        RMTSYS(&MACHINE)

             CHGVAR     VAR(&JOBNAME) VALUE('R' *TCAT &NUMLOTC)

             /* Restauration du programme qui va extraire les objets et les copier à leur  */
             /* destination */
             CHGVAR     VAR(&CMD) VALUE('RSTOBJ OBJ(DSTLOT10) SAVLIB(' *TCAT +
                          &BIBLIO *TCAT ') DEV(*SAVF) SAVF(' *TCAT &BIBLIO *TCAT +
                          '/' *TCAT &SAVF *TCAT ') MBROPT(*NEW) ALWOBJDIF(*ALL)')
             RUNRMTCMD  CMD(&CMD) RMTLOCNAME(&MACHINE *IP) RMTUSER(&USER) +
                          RMTPWD(&MDP)
             MONMSG     MSGID(CPF91CF) EXEC(DO)
                CHGVAR     VAR(&PMGDT) VALUE(&MACHINE)
                CHGVAR     VAR(&PMGID) VALUE('ERR0004')
                CALL       PGM(GSTMSG01) PARM(&PTYP &PFICM &PMGID &PMGDT &PGM)
                GOTO       CMDLBL(FIN)
             ENDDO

             /* Lancement du programme qui va extraire les objets et les copier à leur */
             /* destination                                                            */
             CHGVAR     VAR(&CMD) VALUE('CALL' *BCAT &BIBLIO *TCAT '/DSTLOT10 +
                          PARM(' *TCAT &NUMLOTC *TCAT ')')
             RUNRMTCMD  CMD(&CMD) RMTLOCNAME(&MACHINE *IP) RMTUSER(&USER) +
                          RMTPWD(&MDP)
             MONMSG     MSGID(CPF91CF) EXEC(DO)
                CHGVAR     VAR(&PMGDT) VALUE(&MACHINE)
                CHGVAR     VAR(&PMGID) VALUE('ERR0005')
                CALL       PGM(GSTMSG01) PARM(&PTYP &PFICM &PMGID &PMGDT &PGM)
                GOTO       CMDLBL(FIN)
             ENDDO

             DLTF       FILE(&BIBLIO/&SAVF)

             GOTO       CMDLBL(FIN)

 ERREUR:     DMPCLPGM
             CALLPRC    PRC(gest_erreur)
 FIN:
             ENDPGM
