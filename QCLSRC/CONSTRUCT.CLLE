      /* >>PRE-COMPILER<<                                                 */
      /*   >>CRTCMD<<  CRTCLMOD MODULE(&LI/&OB) SRCFILE(&SL/&SF) +        */
      /*                 SRCMBR(&SM);                                     */
      /*   >>IMPORTANT<<                                                  */
      /*     >>PARM<<  OPTION(*EVENTF);                                   */
      /*     >>PARM<<  DBGVIEW(*ALL);                                     */
      /*   >>END-IMPORTANT<<                                              */
      /*   >>EXECUTE<<                                                    */
      /* >>END-PRE-COMPILER<<                                             */

             PGM        PARM(&PPREBIBSRC &PPREFICSRC &PPRESRCMBR &PBIBDST &POBJDST +
                          &PTYPOBJ &PBIBSRC &PFICSRC &PMBRSRC &OPTIMIZE &FAILED +
                          &FILEATTACH &LISTBIB &GESTDATA)


             DCL        VAR(&PPREBIBSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PPREFICSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PPRESRCMBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PBIBDST) TYPE(*CHAR) LEN(10)
             DCL        VAR(&POBJDST) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PTYPOBJ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PBIBSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PFICSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PMBRSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PREBIBSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PREFICSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRESRCMBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BIBDST) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OBJDST) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TYPOBJ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BIBSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FICSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBRSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OPTIMIZE) TYPE(*CHAR) LEN(20)
             DCL        VAR(&OPTIMIZSQL) TYPE(*CHAR) LEN(20)
             DCL        VAR(&FAILED) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&TYPSRC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NUMALEA) TYPE(*DEC) LEN(15 0)
             DCL        VAR(&NUMALEC) TYPE(*CHAR) LEN(9)
             DCL        VAR(&RAND) TYPE(*DEC) LEN(15 0) VALUE(999999999)
             DCL        VAR(&SAUV_TAB) TYPE(*CHAR) LEN(10) VALUE('          ')
             DCL        VAR(&ATROBJ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QTEMP) TYPE(*CHAR) LEN(10) VALUE('QTEMP')
             DCL        VAR(&EXISTS) TYPE(*LGL)
             DCL        VAR(&OBJET) TYPE(*CHAR) LEN(20)
             DCL        VAR(&TYPOBJE) TYPE(*CHAR) LEN(10) VALUE('*FILE     +
                          ')
             DCL        VAR(&RTRSRCSTD) TYPE(*LGL)
             DCL        VAR(&FILEATTACH) TYPE(*CHAR) LEN(10000)
             DCL        VAR(&LISTBIB) TYPE(*CHAR) LEN(10) /* Liste +
                          de bibliothèque à utiliser avec la +
                          commande POSLIB */
             DCL        VAR(&QUOTE) TYPE(*CHAR) LEN(1) VALUE('''')
             DCL        VAR(&GESTDATA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&OBJETQ) TYPE(*CHAR) LEN(20)
             DCLPRCOPT  ALWRTVSRC(*YES) TEXT('Génération d''un objet avec +
                          STRPREPRC') BNDDIR(I_BUILDER)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ERREUR))

             CHGVAR     VAR(&PREBIBSRC) Value(&PPREBIBSRC)
             CHGVAR     VAR(&PREFICSRC) Value(&PPREFICSRC)
             CHGVAR     VAR(&PRESRCMBR) Value(&PPRESRCMBR)
             CHGVAR     VAR(&BIBDST) Value(&PBIBDST)
             CHGVAR     VAR(&OBJDST) Value(&POBJDST)
             CHGVAR     VAR(&TYPOBJ) Value(&PTYPOBJ)
             CHGVAR     VAR(&BIBSRC) Value(&PBIBSRC)
             CHGVAR     VAR(&FICSRC) Value(&PFICSRC)
             CHGVAR     VAR(&MBRSRC) Value(&PMBRSRC)

             /* Récupération du type de source                             */
             RTVMBRD    FILE(&BIBSRC/&FICSRC) MBR(&MBRSRC *SAME) +
                          SRCTYPE(&TYPSRC)

             POSLIB     LISTE(&LISTBIB) LIB(*NONE)
             MONMSG     MSGID(CPF0000)

             ADDLIBLE   LIB(ISPHERE) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)

             /* En cas de précompilation SQL l'OPTIMIZE doit être mis dans COMPILOPT*/
             /* de la commande de compilation                                       */
             IF         COND(&TYPSRC *EQ 'SQLRPGLE') THEN(DO)
                CHGVAR     VAR(&OPTIMIZSQL) VALUE('''OPTIMIZE(' *TCAT +
                             &OPTIMIZE *TCAT ')''')
                CHGVAR     VAR(&OPTIMIZE) VALUE(&OPTIMIZSQL)
             ENDDO
             /* Si &OPTIMIZE contient *NONE, il faut ajouter des quotes         */
             IF         COND(&OPTIMIZE *EQ '*NONE               ') +
                          THEN(CHGVAR VAR(&OPTIMIZE) VALUE(&QUOTE *TCAT +
                          &OPTIMIZE *TCAT &QUOTE))
             /* Récupération du type d'objet pour s'assurer que s'il existe il  */
             /* faut sauvegarder les données avant création de la table/fichier */
 RTVOBJD:    RTVOBJD    OBJ(&BIBDST/&OBJDST) OBJTYPE(&TYPOBJE) +
                          OBJATR(&ATROBJ)
             MONMSG     MSGID(CPF9812 CPF9801 CPF9811)
             MONMSG     MSGID(CPF3203) EXEC(DO)
                SNDPGMMSG  MSG('''Objet en cours d''utilisation, nouvel +
                             essai dans 30 secondes''')
                DLYJOB     DLY(30)
                GOTO       CMDLBL(RTVOBJD)
             ENDDO
             /* Si le type d'objet est TABLE ou FICHIER, les données sont conservées */
             /* dans un fichier temporaire pour être recopiées en fin de CONSTRUCT   */
             IF         COND(&ATROBJ *EQ 'PF') THEN(DO)
                CHGVAR     VAR(&OBJET) VALUE(&OBJDST *CAT &BIBDST)
                CALLPRC    PRC(EXISTS) PARM((&OBJET) (&TYPOBJE)) +
                             RTNVAL(&EXISTS)
                IF         COND(&EXISTS *EQ '1') THEN(DO)
                CALLPRC    PRC(ALEATOIRE) PARM((&RAND)) RTNVAL(&NUMALEA)
                CHGVAR     VAR(&NUMALEC) VALUE(&NUMALEA)
             CHGVAR     VAR(&SAUV_TAB) VALUE('A' *TCAT &NUMALEC)
             CPYF       FROMFILE(&BIBDST/&OBJDST) TOFILE(&QTEMP/&SAUV_TAB) +
                          FROMMBR(*ALL) TOMBR(*FROMMBR) MBROPT(*REPLACE) +
                          CRTFILE(*YES) ERRLVL(*NOMAX)
             ENDDO
             ENDDO

             /* Création de l'objet                                                */
 STANDARD:   STRPREPRC  USESRCFILE(&PREBIBSRC/&PREFICSRC) +
                          USESRCMBR(&PRESRCMBR) OPTION(*EVENTF) +
                          CHGOBJD(*NO) LIB(&BIBDST) OBJ(&OBJDST) +
                          SRCLIB(&BIBSRC) SRCFILE(&FICSRC) SRCMBR(&MBRSRC) +
                          USER0(&OPTIMIZE)
             /* Au cas où il n'y a pas de scripts spécifique dans le source */
             /* On va rechercher le script standard PRE de création         */
             MONMSG     MSGID(PRZ0028) EXEC(DO)
                CALLPRC    PRC(SOURCE_STANDARD) PARM((&TYPOBJ) (&TYPSRC) +
                             (&PREBIBSRC) (&PREFICSRC) (&PRESRCMBR)) +
                             RTNVAL(&RTRSRCSTD)
                IF         COND(&RTRSRCSTD *EQ '1') THEN(GOTO +
                             CMDLBL(STANDARD))
             ENDDO
             MONMSG     MSGID(PRZ0002) EXEC(DO)
             CALLPRC PRC(RECUP_ERREUR) +
                          PARM((&BIBDST) (&OBJDST))
             CHGVAR     VAR(&FAILED) VALUE(&FAILED + 1)
             ENDDO

             IF         COND(&SAUV_TAB *NE '          ') THEN(DO)
                CPYF       FROMFILE(&QTEMP/&SAUV_TAB) TOFILE(&BIBDST/&OBJDST) +
                             FROMMBR(*ALL) TOMBR(*FROMMBR) MBROPT(*REPLACE) +
                             CRTFILE(*YES) FMTOPT(*MAP *DROP) ERRLVL(*NOMAX)
                MONMSG     MSGID(CPF2817)
                DLTF       FILE(&QTEMP/&SAUV_TAB)
             ENDDO
             RTVOBJD    OBJ(&BIBDST/&OBJDST) OBJTYPE(&TYPOBJE) +
                          OBJATR(&ATROBJ)
             MONMSG     MSGID(CPF9812 CPF9801 CPF9811)
             IF         COND(&ATROBJ *EQ 'LF') THEN(GRTOBJAUT +
                          OBJ(&BIBDST/&OBJDST) OBJTYPE(*FILE) +
                          USER(*PUBLIC) AUT(*ALL))
             CHGVAR     VAR(&OBJETQ) VALUE(&OBJDST *CAT &BIBDST)

             GOTO       CMDLBL(FIN)

 ERREUR:     DMPCLPGM
             CALLPRC    PRC(GEST_ERREUR)


 FIN:        RETURN
             ENDPGM
